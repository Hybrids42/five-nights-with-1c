
#Область ПрограммныйИнтерфейс

// Параметры:
//  ДанныеЭлемента - Структура
Процедура ПриОбработкеЭлементаОчереди(ДанныеЭлемента) Экспорт
		
	Если ДанныеЭлемента.Свойство("message") Тогда
		
		Сообщение = ДанныеЭлемента.message;
	
		Если Не Сообщение.Свойство("from") Тогда
			Возврат;
		КонецЕсли;
		
		Если Не Сообщение.Свойство("text") Тогда
			Возврат;
		КонецЕсли;
		
		Отправитель = Сообщение.from.id;
		Текст = Сообщение.text;
	
		Если СтрНачинаетсяС(Текст, "/start") Тогда
			НачатьИгру(Отправитель);
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ДанныеЭлемента.Свойство("callback_query") Тогда
		
		НажатиеКнопки = ДанныеЭлемента.callback_query;
		ОбработатьНажатиеКнопки(НажатиеКнопки);
		
	КонецЕсли;
	
	// ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, Текст);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НачатьИгру(Отправитель)

	Вопрос = ПолучитьВопросПоКоду("000000001");
	Если Вопрос = Неопределено тогда
		ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, "нет пролога");
	КонецЕсли;
	
	ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();	
	ДополнительныеСвойства.Клавиатура = ВстроеннаяКлавиатураОтветов(Вопрос.Ответы.Выбрать(), Вопрос.Ссылка);
	
	ЭлементыВопроса = Новый Массив;
	
	ЭлементыВопроса.Добавить(Вопрос.Текст);
	
	ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, Вопрос.Текст, ДополнительныеСвойства);

КонецПроцедуры

Функция ВстроеннаяКлавиатураОтветов(Ответы, Ссылка)
	
	ИндексКнопки = 0;
	СтрокиКлавиатуры = Новый Массив;
	СтрокаКлавиатуры = Новый Массив;
	
	Пока Ответы.Следующий() Цикл
		
		ИндексКнопки = ИндексКнопки + 1;
		
		ДанныеКнопки = Строка(Ссылка.Код) + "|" + 
					   Строка(Ответы.Следующее.Код);
		
		Кнопка = Новый Структура;
		Кнопка.Вставить("text", Ответы.Текст);
		Кнопка.Вставить("callback_data", ДанныеКнопки);
		
		СтрокаКлавиатуры.Добавить(Кнопка);
		
		Если ИндексКнопки >= 2 Тогда
			ИндексКнопки = 0;
			СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
			СтрокаКлавиатуры = Новый Массив;
		КонецЕсли;		
		
	КонецЦикла;
	
	Если СтрокаКлавиатуры.Количество() > 0 Тогда
		СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
	КонецЕсли;		
	
	Клавиатура = Новый Структура;
	Клавиатура.Вставить("inline_keyboard", СтрокиКлавиатуры);
	
	Возврат Клавиатура;
	
КонецФункции

Процедура ОбработатьНажатиеКнопки(НажатиеКнопки)
	
	Если Не НажатиеКнопки.Свойство("from") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НажатиеКнопки.Свойство("data") Тогда
		Возврат;
	КонецЕсли;
	
	Получатель = НажатиеКнопки.from.id;
	Команда = НажатиеКнопки.data;
	ИдентификаторСообщения = НажатиеКнопки.message.message_id;
	
	ЧастиКоманды = СтрРазделить(Команда, "|");
	
	ИдентификаторВопроса = ЧастиКоманды[0];
	СледующийВопрос = ЧастиКоманды[1];
	
	ПредыдущийВопрос = ПолучитьВопросПоКоду(ИдентификаторВопроса);
	Если ПредыдущийВопрос = Неопределено тогда
		ИнтеграцияТелеграм.ОтправитьСообщение(Получатель, "нет вопросса с кодом " + ИдентификаторВопроса);
		Возврат;
	КонецЕсли;
	
	Вопрос = ПолучитьВопросПоКоду(СледующийВопрос);
	Если Вопрос = Неопределено тогда
		ИнтеграцияТелеграм.ОтправитьСообщение(Получатель, "нет вопросса с кодом " + СледующийВопрос);
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();	
	ДополнительныеСвойства.Клавиатура = ВстроеннаяКлавиатураОтветов(Вопрос.Ответы.Выбрать(), Вопрос.Ссылка);
	
	ЭлементыВопроса = Новый Массив;
	
	ЭлементыВопроса.Добавить(Вопрос.Текст);
	
	ИнтеграцияТелеграм.РедактироватьСообщение(Получатель, ИдентификаторСообщения, ПредыдущийВопрос.Текст);
	ИнтеграцияТелеграм.ОтправитьСообщение(Получатель, Вопрос.Текст, ДополнительныеСвойства);
	
КонецПроцедуры

Функция ПолучитьВопросПоКоду(Код)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВопросыИгры.Ссылка,
		|	ВопросыИгры.Текст,
		|	ВопросыИгры.Ответы.(
		|		Текст,
		|		Следующее)
		|ИЗ
		|	Справочник.ВопросыИгры КАК ВопросыИгры
		|ГДЕ
		|	ВопросыИгры.Код = &Код";
		
	Запрос.УстановитьПараметр("Код", Код);	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Выборка;
	
КонецФункции

#КонецОбласти
